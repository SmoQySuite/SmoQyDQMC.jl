<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Square Hubbard-Holstein Model · SmoQyDQMC.jl</title><script data-outdated-warner src="../../assets/warner.js"></script><link rel="canonical" href="https://SmoQySuite.github.io/SmoQyDQMC.jl/examples/hubbard_holstein_square/"/><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.045/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.24/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../">SmoQyDQMC.jl</a></span></div><form class="docs-search" action="../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../">Home</a></li><li><a class="tocitem" href="../../hamiltonian/">Supported Hamiltonians</a></li><li><a class="tocitem" href="../../simulation_output/">Simulation Output Overview</a></li><li><a class="tocitem" href="../../api/">API</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../hubbard_chain/">Hubbard Chain</a></li><li><a class="tocitem" href="../hubbard_chain_mpi/">Hubbard Chain with MPI</a></li><li><a class="tocitem" href="../hubbard_chain_checkpoint/">Hubbard Chain with Checkpointing</a></li><li><a class="tocitem" href="../holstein_chain/">Holstein Chain with Density Tuning</a></li><li><a class="tocitem" href="../ossh_chain/">Optical Su-Schrieffer-Heeger Chain</a></li><li><a class="tocitem" href="../bssh_chain/">Bond Su-Schrieffer-Heeger Chain</a></li><li class="is-active"><a class="tocitem" href>Square Hubbard-Holstein Model</a></li><li><a class="tocitem" href="../hubbard_threeband/">Three-Band Hubbard Model</a></li><li><a class="tocitem" href="../holstein_kagome/">Kagome Holstein Model with Density Tuning</a></li><li><a class="tocitem" href="../hubbard_honeycomb/">Honeycomb Hubbard model</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>Square Hubbard-Holstein Model</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Square Hubbard-Holstein Model</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/SmoQySuite/SmoQyDQMC.jl/blob/main/literate_scripts/hubbard_holstein_square.jl#" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Square-Hubbard-Holstein-Model"><a class="docs-heading-anchor" href="#Square-Hubbard-Holstein-Model">Square Hubbard-Holstein Model</a><a id="Square-Hubbard-Holstein-Model-1"></a><a class="docs-heading-anchor-permalink" href="#Square-Hubbard-Holstein-Model" title="Permalink"></a></h1><p>In this example we write a script to simulate the Hubbard-Holstein model on a square lattice, with a Hamiltonian given by</p><p class="math-container">\[\begin{align*}
\hat{H} = &amp; -t \sum_{\sigma,\langle i, j \rangle} (\hat{c}^{\dagger}_{\sigma,i}, \hat{c}^{\phantom \dagger}_{\sigma,j} + {\rm h.c.})
            -\mu \sum_{\sigma,i}\hat{n}_{\sigma,i}\\
          &amp; + U \sum_{i} (\hat{n}_{\uparrow,i}-\tfrac{1}{2})(\hat{n}_{\downarrow,i}-\tfrac{1}{2})
            + \alpha \sum_{\sigma,i} \hat{X}_i (\hat{n}_{\sigma,i} - \tfrac{1}{2}) \\
          &amp; + \sum_i \left( \frac{1}{2M}\hat{P}_i^2 + \frac{1}{2}M\Omega^2\hat{X}_i^2 \right),
\end{align*}\]</p><p>where <span>$\hat{c}^\dagger_{\sigma,i} \ (\hat{c}^{\phantom \dagger}_{\sigma,i})$</span> creates (annihilates) a spin <span>$\sigma$</span> electron on site <span>$i$</span> in the lattice, and <span>$\hat{n}_{\sigma,i} = \hat{c}^\dagger_{\sigma,i} \hat{c}^{\phantom \dagger}_{\sigma,i}$</span> is the spin-<span>$\sigma$</span> electron number operator for site <span>$i$</span>. The nearest-neighbor hopping amplitude is <span>$t$</span> and <span>$\mu$</span> is the chemical potential. The strength of the repulsive Hubbard interaction is controlled by <span>$U&gt;0$</span>. <span>$\hat{X}_i \ (\hat{P}_i)$</span> is the phonon position (momentum) operator for a dispersionless mode placed on site <span>$i$</span> with phonon frequency <span>$\Omega$</span> and corresponding ion mass <span>$M$</span>. The stength of the Holstein electron-phonon is controlled by the parameter <span>$\alpha$</span>.</p><p>The example script to simulate this sytem is <a href="https://github.com/SmoQySuite/SmoQyDQMC.jl/blob/main/scripts/hubbard_holstein_square.jl"><code>scripts/hubbard_holstein_square.jl</code></a>. A short test simulation using this script that only takes a few minutes on most personal computers is</p><pre><code class="nohighlight hljs">&gt; julia hubbard_holstein_square.jl 0 6.0 0.1 0.1 0.0 4.0 4 1000 5000 50</code></pre><p>which simulates the Hubbard-Holstein model on a <span>$L = 4$</span> square lattice, with <span>$U = 6.0$</span>, <span>$\Omega = 0.1$</span>, <span>$\alpha = 0.1$</span> and <span>$\mu = 0.0$</span> at an inverse temperature of <span>$\beta = 4.0$</span>. In this simulation the Hubbard-Stranonovich and phonon fields are thermalized with <code>N_burnin = 1000</code> rounds of updates, followed by <code>N_udpates = 5000</code> rounds of updates with measurements being made. Bin averaged measurements are written to file <code>N_bins = 50</code> during the simulation.</p><p>Below you will find a more heavily commented version of the <a href="https://github.com/SmoQySuite/SmoQyDQMC.jl/blob/main/example_scripts/hubbard_holstein_square.jl"><code>example_scripts/hubbard_holstein_square.jl</code></a> script that includes additional exposition on what each part of the code is doing. Additionally, this script demonstrates how to calculate the extended s-wave and d-wave pair susceptibilities.</p><pre><code class="language-julia hljs">using LinearAlgebra
using Random
using Printf

using SmoQyDQMC
import SmoQyDQMC.LatticeUtilities  as lu
import SmoQyDQMC.JDQMCFramework    as dqmcf
import SmoQyDQMC.JDQMCMeasurements as dqmcm

# Define top-level function for running DQMC simulation
function run_hubbard_holstein_square_simulation(sID, U, Ω, α, μ, β, L, N_burnin, N_updates, N_bins; filepath = &quot;.&quot;)

    # Construct the foldername the data will be written to.
    datafolder_prefix = @sprintf &quot;hubbard_chain_U%.2f_w%.2f_a%.2f_mu%.2f_L%d_b%.2f&quot; U Ω α μ L β

    # Initialize an instance of the SimulationInfo type.
    simulation_info = SimulationInfo(
        filepath = filepath,
        datafolder_prefix = datafolder_prefix,
        sID = sID
    )

    # Initialize the directory the data will be written to.
    initialize_datafolder(simulation_info)

    # Initialize a random number generator that will be used throughout the simulation.
    seed = abs(rand(Int))
    rng = Xoshiro(seed)

    # Set the discretization in imaginary time for the DQMC simulation.
    Δτ = 0.10

    # Calculate the length of the imaginary time axis, Lτ = β/Δτ.
    Lτ = dqmcf.eval_length_imaginary_axis(β, Δτ)

    # This flag indicates whether or not to use the checkboard approximation to
    # represent the exponentiated hopping matrix exp(-Δτ⋅K)
    checkerboard = false

    # Whether the propagator matrices should be represented using the
    # symmetric form B = exp(-Δτ⋅K/2)⋅exp(-Δτ⋅V)⋅exp(-Δτ⋅K/2)
    # or the asymetric form B = exp(-Δτ⋅V)⋅exp(-Δτ⋅K)
    symmetric = false

    # Set the initial period in imaginary time slices with which the Green&#39;s function matrices
    # will be recomputed using a numerically stable procedure.
    n_stab = 10

    # Specify the maximum allowed error in any element of the Green&#39;s function matrix that is
    # corrected by performing numerical stabiliziation.
    δG_max = 1e-6

    # Calculate the bins size.
    bin_size = div(N_updates, N_bins)

    # Fermionic time-step used in HMC update.
    Δt = 1/(10*Ω)

    # Number of fermionic time-steps in HMC update.
    Nt = 10

    # Number of bosonic time-steps per fermionic time-step in HMC udpate.
    nt = 10

    # Regularizaton parameter for fourier acceleration mass matrix used in HMC dyanmics.
    reg = 1.0

    # Initialize a dictionary to store additional information about the simulation.
    additional_info = Dict(
        &quot;dG_max&quot; =&gt; δG_max,
        &quot;N_burnin&quot; =&gt; N_burnin,
        &quot;N_updates&quot; =&gt; N_updates,
        &quot;N_bins&quot; =&gt; N_bins,
        &quot;bin_size&quot; =&gt; bin_size,
        &quot;local_acceptance_rate&quot; =&gt; 0.0,
        &quot;hmc_acceptance_rate&quot; =&gt; 0.0,
        &quot;reflection_acceptance_rate&quot; =&gt; 0.0,
        &quot;n_stab_init&quot; =&gt; n_stab,
        &quot;symmetric&quot; =&gt; symmetric,
        &quot;checkerboard&quot; =&gt; checkerboard,
        &quot;seed&quot; =&gt; seed,
        &quot;dt&quot; =&gt; Δt,
        &quot;Nt&quot; =&gt; Nt,
        &quot;nt&quot; =&gt; nt,
        &quot;reg&quot; =&gt; reg
    )

    #######################
    ### DEFINE THE MODEL ##
    #######################

    # Initialize an instance of the type UnitCell.
    unit_cell = lu.UnitCell(lattice_vecs = [[1.0, 0.0],
                                            [0.0, 1.0]],
                            basis_vecs   = [[0.0, 0.0]])

    # Initialize an instance of the type Lattice.
    lattice = lu.Lattice(
        L = [L, L],
        periodic = [true, true]
    )

    # Initialize an instance of the ModelGeometry type.
    model_geometry = ModelGeometry(unit_cell, lattice)

    # Get the number of orbitals in the lattice.
    N = lu.nsites(unit_cell, lattice)

    # Define the nearest-neighbor bond in the +x direction.
    bond_px = lu.Bond(orbitals = (1,1), displacement = [1,0])

    # Add nearest-neighbor bond in the +x direction.
    bond_px_id = add_bond!(model_geometry, bond_px)

    # Define the nearest-neighbor bond in the +y direction.
    bond_py = lu.Bond(orbitals = (1,1), displacement = [0,1])

    # Add the nearest-neighbor bond in the +y direction.
    bond_py_id = add_bond!(model_geometry, bond_py)

    # Here we define bonds to points in the negative x and y directions respectively.
    # We do this in order to be able to measure all the pairing channels we need
    # in order to reconstruct the extended s-wave and d-wave pair susceptibilities.

    # Define the nearest-neighbor bond in the +x direction.
    bond_nx = lu.Bond(orbitals = (1,1), displacement = [-1,0])

    # Add nearest-neighbor bond in the +x direction.
    bond_nx_id = add_bond!(model_geometry, bond_nx)

    # Define the nearest-neighbor bond in the +y direction.
    bond_ny = lu.Bond(orbitals = (1,1), displacement = [0,-1])

    # Add the nearest-neighbor bond in the +y direction.
    bond_ny_id = add_bond!(model_geometry, bond_ny)

    # Define nearest-neighbor hopping amplitude, setting the energy scale for the system.
    t = 1.0

    # Define the tight-binding model
    tight_binding_model = TightBindingModel(
        model_geometry = model_geometry,
        t_bonds = [bond_px, bond_py], # defines hopping
        t_mean = [t, t],            # defines corresponding hopping amplitude
        μ = μ,                      # set chemical potential
        ϵ_mean = [0.]               # set the (mean) on-site energy
    )

    # Initialize the Hubbard interaction in the model.
    hubbard_model = HubbardModel(
        shifted = false,
        U_orbital = [1],
        U_mean = [U],
    )

    # Initialize a null electron-phonon model.
    electron_phonon_model = ElectronPhononModel(
        model_geometry = model_geometry,
        tight_binding_model = tight_binding_model
    )

    # Define a dispersionless electron-phonon mode to live on each site in the lattice.
    phonon = PhononMode(orbital = 1, Ω_mean = Ω)

    # Add the phonon mode definition to the electron-phonon model.
    phonon_id = add_phonon_mode!(
        electron_phonon_model = electron_phonon_model,
        phonon_mode = phonon
    )

    # Define a on-site Holstein coupling between the electron and the local dispersionless phonon mode.
    holstein_coupling = HolsteinCoupling(
    	model_geometry = model_geometry,
    	phonon_mode = phonon_id,
    	bond = lu.Bond(orbitals = (1,1), displacement = [0,0]),
    	α_mean = α
    )

    # Add the Holstein coupling definition to the model.
    holstein_coupling_id = add_holstein_coupling!(
    	electron_phonon_model = electron_phonon_model,
    	holstein_coupling = holstein_coupling,
    	model_geometry = model_geometry
    )

    # Write the model summary to file.
    model_summary(
        simulation_info = simulation_info,
        β = β, Δτ = Δτ,
        model_geometry = model_geometry,
        tight_binding_model = tight_binding_model,
        interactions = (hubbard_model, electron_phonon_model)
    )

    #########################################
    ### INITIALIZE FINITE MODEL PARAMETERS ##
    #########################################

    # Initialize tight-binding parameters.
    tight_binding_parameters = TightBindingParameters(
        tight_binding_model = tight_binding_model,
        model_geometry = model_geometry,
        rng = rng
    )

    # Initialize Hubbard interaction parameters.
    hubbard_parameters = HubbardParameters(
        model_geometry = model_geometry,
        hubbard_model = hubbard_model,
        rng = rng
    )

    # Apply Ising Hubbard-Stranonvich (HS) transformation, and initialize
    # corresponding HS fields that will be sampled in DQMC simulation.
    hubbard_ising_parameters = HubbardIsingHSParameters(
        β = β, Δτ = Δτ,
        hubbard_parameters = hubbard_parameters,
        rng = rng
    )

    # Initialize electron-phonon parameters.
    electron_phonon_parameters = ElectronPhononParameters(
        β = β, Δτ = Δτ,
        electron_phonon_model = electron_phonon_model,
        tight_binding_parameters = tight_binding_parameters,
        model_geometry = model_geometry,
        rng = rng
    )

    ##############################
    ### INITIALIZE MEASUREMENTS ##
    ##############################

    # Initialize the container that measurements will be accumulated into.
    measurement_container = initialize_measurement_container(model_geometry, β, Δτ)

    # Initialize the tight-binding model related measurements, like the hopping energy.
    initialize_measurements!(measurement_container, tight_binding_model)

    # Initialize the Hubbard interaction related measurements.
    initialize_measurements!(measurement_container, hubbard_model)

    # Initialize the electron-phonon interaction related measurements.
    initialize_measurements!(measurement_container, electron_phonon_model)

    # Initialize the single-particle electron Green&#39;s function measurement.
    initialize_correlation_measurements!(
        measurement_container = measurement_container,
        model_geometry = model_geometry,
        correlation = &quot;greens&quot;,
        time_displaced = true,
        pairs = [(1, 1)]
    )

    # Initialize the phonon Green&#39;s function measurement.
    initialize_correlation_measurements!(
        measurement_container = measurement_container,
        model_geometry = model_geometry,
        correlation = &quot;phonon_greens&quot;,
        time_displaced = true,
        pairs = [(1, 1)]
    )

    # Initialize density correlation function measurement.
    initialize_correlation_measurements!(
        measurement_container = measurement_container,
        model_geometry = model_geometry,
        correlation = &quot;density&quot;,
        time_displaced = false,
        integrated = true,
        pairs = [(1, 1)]
    )

    # Initialize the spin-z correlation function measurement.
    initialize_correlation_measurements!(
        measurement_container = measurement_container,
        model_geometry = model_geometry,
        correlation = &quot;spin_z&quot;,
        time_displaced = false,
        integrated = true,
        pairs = [(1, 1)]
    )

    # Measure all possible combinations of bond pairing channels
    # for the bonds we have defined. We will need each of these
    # pairs channels measured in order to reconstruct the extended
    # s-wave and d-wave pair susceptibilities.
    # Initialize the pair correlation function measurement.
    initialize_correlation_measurements!(
        measurement_container = measurement_container,
        model_geometry = model_geometry,
        correlation = &quot;pair&quot;,
        time_displaced = false,
        integrated = true,
        pairs = [(1, 1),
                 (bond_px_id, bond_px_id),  (bond_px_id, bond_nx_id),
                 (bond_nx_id, bond_px_id), (bond_nx_id, bond_nx_id),
                 (bond_py_id, bond_py_id),  (bond_py_id, bond_ny_id),
                 (bond_ny_id, bond_py_id), (bond_ny_id, bond_ny_id),
                 (bond_px_id, bond_py_id),  (bond_px_id, bond_ny_id),
                 (bond_nx_id, bond_py_id), (bond_nx_id, bond_ny_id),
                 (bond_py_id, bond_px_id),  (bond_py_id, bond_nx_id),
                 (bond_ny_id, bond_px_id), (bond_ny_id, bond_nx_id)]
    )

    # Initialize the current correlation function measurement
    initialize_correlation_measurements!(
        measurement_container = measurement_container,
        model_geometry = model_geometry,
        correlation = &quot;current&quot;,
        time_displaced = false,
        integrated = true,
        pairs = [(1, 1), # hopping ID pair for x-direction hopping
                 (2, 2)] # hopping ID pair for y-direction hopping
    )

    # Initialize the sub-directories to which the various measurements will be written.
    initialize_measurement_directories(
        simulation_info = simulation_info,
        measurement_container = measurement_container
    )

    #############################
    ### SET-UP DQMC SIMULATION ##
    #############################

    # Allocate FermionPathIntegral type for both the spin-up and spin-down electrons.
    fermion_path_integral_up = FermionPathIntegral(tight_binding_parameters = tight_binding_parameters, β = β, Δτ = Δτ)
    fermion_path_integral_dn = FermionPathIntegral(tight_binding_parameters = tight_binding_parameters, β = β, Δτ = Δτ)

    # Initialize the FermionPathIntegral type for both the spin-up and spin-down electrons.
    initialize!(fermion_path_integral_up, fermion_path_integral_dn, hubbard_parameters)
    initialize!(fermion_path_integral_up, fermion_path_integral_dn, hubbard_ising_parameters)

    # Initialize the imaginary-time propagators for each imaginary-time slice for both the
    # spin-up and spin-down electrons.
    Bup = initialize_propagators(fermion_path_integral_up, symmetric=symmetric, checkerboard=checkerboard)
    Bdn = initialize_propagators(fermion_path_integral_dn, symmetric=symmetric, checkerboard=checkerboard)

    # Initialize FermionGreensCalculator for the spin-up and spin-down electrons.
    fermion_greens_calculator_up = dqmcf.FermionGreensCalculator(Bup, β, Δτ, n_stab)
    fermion_greens_calculator_dn = dqmcf.FermionGreensCalculator(Bdn, β, Δτ, n_stab)

    # Initialize alternate fermion greens calculator required for performing various global updates.
    fermion_greens_calculator_up_alt = dqmcf.FermionGreensCalculator(fermion_greens_calculator_up)
    fermion_greens_calculator_dn_alt = dqmcf.FermionGreensCalculator(fermion_greens_calculator_dn)

    # Allcoate matrices for spin-up and spin-down electron Green&#39;s function matrices.
    Gup = zeros(eltype(Bup[1]), size(Bup[1]))
    Gdn = zeros(eltype(Bdn[1]), size(Bdn[1]))

    # Initialize the spin-up and spin-down electron Green&#39;s function matrices, also
    # calculating their respective determinants as the same time.
    logdetGup, sgndetGup = dqmcf.calculate_equaltime_greens!(Gup, fermion_greens_calculator_up)
    logdetGdn, sgndetGdn = dqmcf.calculate_equaltime_greens!(Gdn, fermion_greens_calculator_dn)

    # Initialize Hamitlonian/Hybrid monte carlo (HMC) updater.
    hmc_updater = HMCUpdater(
        electron_phonon_parameters = electron_phonon_parameters,
        G = Gup, Nt = Nt, Δt = Δt, nt = nt, reg = reg
    )

    # Allocate matrices for various time-displaced Green&#39;s function matrices.
    Gup_ττ = similar(Gup) # G↑(τ,τ)
    Gup_τ0 = similar(Gup) # G↑(τ,0)
    Gup_0τ = similar(Gup) # G↑(0,τ)
    Gdn_ττ = similar(Gdn) # G↓(τ,τ)
    Gdn_τ0 = similar(Gdn) # G↓(τ,0)
    Gdn_0τ = similar(Gdn) # G↓(0,τ)

    # Initialize variables to keep track of the largest numerical error in the
    # Green&#39;s function matrices corrected by numerical stabalization.
    δG = zero(typeof(logdetGup))
    δθ = zero(typeof(sgndetGup))

    ####################################
    ### BURNIN/THERMALIZATION UPDATES ##
    ####################################

    # Iterate over burnin/thermalization updates.
    for n in 1:N_burnin

        # Perform a reflection update.
        (accepted, logdetGup, sgndetGup, logdetGdn, sgndetGdn) = reflection_update!(
            Gup, logdetGup, sgndetGup,
            Gdn, logdetGdn, sgndetGdn,
            electron_phonon_parameters,
            fermion_path_integral_up = fermion_path_integral_up,
            fermion_path_integral_dn = fermion_path_integral_dn,
            fermion_greens_calculator_up = fermion_greens_calculator_up,
            fermion_greens_calculator_dn = fermion_greens_calculator_dn,
            fermion_greens_calculator_up_alt = fermion_greens_calculator_up_alt,
            fermion_greens_calculator_dn_alt = fermion_greens_calculator_dn_alt,
            Bup = Bup, Bdn = Bdn, rng = rng, phonon_types = (phonon_id,)
        )

        # Record whether the reflection update was accepted or rejected.
        additional_info[&quot;reflection_acceptance_rate&quot;] += accepted

        # Perform an HMC update.
        (accepted, logdetGup, sgndetGup, logdetGdn, sgndetGdn, δG, δθ) = hmc_update!(
            Gup, logdetGup, sgndetGup,
            Gdn, logdetGdn, sgndetGdn,
            electron_phonon_parameters,
            hmc_updater,
            fermion_path_integral_up = fermion_path_integral_up,
            fermion_path_integral_dn = fermion_path_integral_dn,
            fermion_greens_calculator_up = fermion_greens_calculator_up,
            fermion_greens_calculator_dn = fermion_greens_calculator_dn,
            fermion_greens_calculator_up_alt = fermion_greens_calculator_up_alt,
            fermion_greens_calculator_dn_alt = fermion_greens_calculator_dn_alt,
            Bup = Bup, Bdn = Bdn,
            δG_max = δG_max, δG = δG, δθ = δθ, rng = rng,
            initialize_force = true
        )

        # Record whether the HMC update was accepted or rejected.
        additional_info[&quot;hmc_acceptance_rate&quot;] += accepted

        # Perform a sweep through the lattice, attemping an update to each Ising HS field.
        (acceptance_rate, logdetGup, sgndetGup, logdetGdn, sgndetGdn, δG, δθ) = local_updates!(
            Gup, logdetGup, sgndetGup, Gdn, logdetGdn, sgndetGdn,
            hubbard_ising_parameters,
            fermion_path_integral_up = fermion_path_integral_up,
            fermion_path_integral_dn = fermion_path_integral_dn,
            fermion_greens_calculator_up = fermion_greens_calculator_up,
            fermion_greens_calculator_dn = fermion_greens_calculator_dn,
            Bup = Bup, Bdn = Bdn, δG_max = δG_max, δG = δG, δθ = δθ, rng = rng
        )

        # Record the acceptance rate for the attempted local updates to the HS fields.
        additional_info[&quot;local_acceptance_rate&quot;] += acceptance_rate
    end

    ################################
    ### START MAKING MEAUSREMENTS ##
    ################################

    # Re-initialize variables to keep track of the largest numerical error in the
    # Green&#39;s function matrices corrected by numerical stabalization.
    δG = zero(typeof(logdetGup))
    δθ = zero(typeof(sgndetGup))

    # Iterate over the number of bin, i.e. the number of time measurements will be dumped to file.
    for bin in 1:N_bins

        # Iterate over the number of updates and measurements performed in the current bin.
        for n in 1:bin_size

            # Perform a reflection update.
            (accepted, logdetGup, sgndetGup, logdetGdn, sgndetGdn) = reflection_update!(
                Gup, logdetGup, sgndetGup,
                Gdn, logdetGdn, sgndetGdn,
                electron_phonon_parameters,
                fermion_path_integral_up = fermion_path_integral_up,
                fermion_path_integral_dn = fermion_path_integral_dn,
                fermion_greens_calculator_up = fermion_greens_calculator_up,
                fermion_greens_calculator_dn = fermion_greens_calculator_dn,
                fermion_greens_calculator_up_alt = fermion_greens_calculator_up_alt,
                fermion_greens_calculator_dn_alt = fermion_greens_calculator_dn_alt,
                Bup = Bup, Bdn = Bdn, rng = rng, phonon_types = (phonon_id,)
            )

            # Record whether the reflection update was accepted or rejected.
            additional_info[&quot;reflection_acceptance_rate&quot;] += accepted

            # Perform an HMC update.
            (accepted, logdetGup, sgndetGup, logdetGdn, sgndetGdn, δG, δθ) = hmc_update!(
                Gup, logdetGup, sgndetGup,
                Gdn, logdetGdn, sgndetGdn,
                electron_phonon_parameters,
                hmc_updater,
                fermion_path_integral_up = fermion_path_integral_up,
                fermion_path_integral_dn = fermion_path_integral_dn,
                fermion_greens_calculator_up = fermion_greens_calculator_up,
                fermion_greens_calculator_dn = fermion_greens_calculator_dn,
                fermion_greens_calculator_up_alt = fermion_greens_calculator_up_alt,
                fermion_greens_calculator_dn_alt = fermion_greens_calculator_dn_alt,
                Bup = Bup, Bdn = Bdn,
                δG_max = δG_max, δG = δG, δθ = δθ, rng = rng,
                initialize_force = true
            )

            # Record whether the HMC update was accepted or rejected.
            additional_info[&quot;hmc_acceptance_rate&quot;] += accepted

            # Perform a sweep through the lattice, attemping an update to each Ising HS field.
            (acceptance_rate, logdetGup, sgndetGup, logdetGdn, sgndetGdn, δG, δθ) = local_updates!(
                Gup, logdetGup, sgndetGup, Gdn, logdetGdn, sgndetGdn,
                hubbard_ising_parameters,
                fermion_path_integral_up = fermion_path_integral_up,
                fermion_path_integral_dn = fermion_path_integral_dn,
                fermion_greens_calculator_up = fermion_greens_calculator_up,
                fermion_greens_calculator_dn = fermion_greens_calculator_dn,
                Bup = Bup, Bdn = Bdn, δG_max = δG_max, δG = δG, δθ = δθ, rng = rng
            )

            # Record the acceptance rate for the attempted local updates to the HS fields.
            additional_info[&quot;local_acceptance_rate&quot;] += acceptance_rate

            # Make measurements, with the results being added to the measurement container.
            (logdetGup, sgndetGup, logdetGdn, sgndetGdn, δG, δθ) = make_measurements!(
                measurement_container,
                logdetGup, sgndetGup, Gup, Gup_ττ, Gup_τ0, Gup_0τ,
                logdetGdn, sgndetGdn, Gdn, Gdn_ττ, Gdn_τ0, Gdn_0τ,
                fermion_path_integral_up = fermion_path_integral_up,
                fermion_path_integral_dn = fermion_path_integral_dn,
                fermion_greens_calculator_up = fermion_greens_calculator_up,
                fermion_greens_calculator_dn = fermion_greens_calculator_dn,
                Bup = Bup, Bdn = Bdn, δG_max = δG_max, δG = δG, δθ = δθ,
                model_geometry = model_geometry, tight_binding_parameters = tight_binding_parameters,
                coupling_parameters = (hubbard_parameters, electron_phonon_parameters)
            )
        end

        # Write the average measurements for the current bin to file.
        write_measurements!(
            measurement_container = measurement_container,
            simulation_info = simulation_info,
            model_geometry = model_geometry,
            bin = bin,
            bin_size = bin_size,
            Δτ = Δτ
        )
    end

    # Calculate acceptance rates.
    additional_info[&quot;hmc_acceptance_rate&quot;] /= (N_updates + N_burnin)
    additional_info[&quot;reflection_acceptance_rate&quot;] /= (N_updates + N_burnin)
    additional_info[&quot;local_acceptance_rate&quot;] /= (N_updates + N_burnin)

    # Record the final numerical stabilization period that the simulation settled on.
    additional_info[&quot;n_stab_final&quot;] = fermion_greens_calculator_up.n_stab

    # Record the maximum numerical error corrected by numerical stablization.
    additional_info[&quot;dG&quot;] = δG

    #################################
    ### PROCESS SIMULATION RESULTS ##
    #################################

    # Process the simulation results, calculating final error bars for all measurements,
    # writing final statisitics to CSV files.
    process_measurements(simulation_info.datafolder, N_bins)

    # Here we use the `composite_correlation_stats` to reconstruct the extended
    # s-wave and d-wave pair susceptibilities. We also subract off the background
    # signal associated with the zero-momentum transfer charge susceptibility.
    # Behind the scenes, uses the binning
    # method to calculate the error bars by calculating both susceptibilities for
    # each bin of data that was written to file.

    # Measure the extended s-wave pair susceptibility.
    Pes, ΔPes = composite_correlation_stat(
        folder = simulation_info.datafolder,
        correlations = [&quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;,
                        &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;,
                        &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;,
                        &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;],
        spaces = [&quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;,
                  &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;,
                  &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;,
                  &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;],
        types = [&quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;,
                 &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;,
                 &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;,
                 &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;],
        ids = [(bond_px_id, bond_px_id), (bond_nx_id, bond_nx_id), (bond_px_id, bond_nx_id), (bond_nx_id, bond_px_id),
               (bond_py_id, bond_py_id), (bond_ny_id, bond_ny_id), (bond_py_id, bond_ny_id), (bond_ny_id, bond_py_id),
               (bond_px_id, bond_py_id), (bond_nx_id, bond_ny_id), (bond_px_id, bond_ny_id), (bond_nx_id, bond_py_id),
               (bond_py_id, bond_px_id), (bond_ny_id, bond_nx_id), (bond_py_id, bond_nx_id), (bond_ny_id, bond_px_id)],
        locs = [(0,0), (0,0), (0,0), (0,0),
                (0,0), (0,0), (0,0), (0,0),
                (0,0), (0,0), (0,0), (0,0),
                (0,0), (0,0), (0,0), (0,0)],
        num_bins = N_bins,
        f = (P_px_px, P_nx_nx, P_px_nx, P_nx_px,
             P_py_py, P_ny_ny, P_py_ny, P_ny_py,
             P_px_py, P_nx_ny, P_px_ny, P_nx_py,
             P_py_px, P_ny_nx, P_py_nx, P_ny_px) -&gt; (P_px_px + P_nx_nx + P_px_nx + P_nx_px +
                                                     P_py_py + P_ny_ny + P_py_ny + P_ny_py +
                                                     P_px_py + P_nx_ny + P_px_ny + P_nx_py +
                                                     P_py_px + P_ny_nx + P_py_nx + P_ny_px)/4
    )
    additional_info[&quot;P_ext-s_avg&quot;] = Pes
    additional_info[&quot;P_ext-s_err&quot;] = ΔPes

    # Measure the d-wave pair susceptibility.
    Pd, ΔPd = composite_correlation_stat(
        folder = simulation_info.datafolder,
        correlations = [&quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;,
                        &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;,
                        &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;,
                        &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;, &quot;pair&quot;],
        spaces = [&quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;,
                  &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;,
                  &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;,
                  &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;, &quot;momentum&quot;],
        types = [&quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;,
                 &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;,
                 &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;,
                 &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;, &quot;integrated&quot;],
        ids = [(bond_px_id, bond_px_id), (bond_nx_id, bond_nx_id), (bond_px_id, bond_nx_id), (bond_nx_id, bond_px_id),
               (bond_py_id, bond_py_id), (bond_ny_id, bond_ny_id), (bond_py_id, bond_ny_id), (bond_ny_id, bond_py_id),
               (bond_px_id, bond_py_id), (bond_nx_id, bond_ny_id), (bond_px_id, bond_ny_id), (bond_nx_id, bond_py_id),
               (bond_py_id, bond_px_id), (bond_ny_id, bond_nx_id), (bond_py_id, bond_nx_id), (bond_ny_id, bond_px_id)],
        locs = [(0,0), (0,0), (0,0), (0,0),
                (0,0), (0,0), (0,0), (0,0),
                (0,0), (0,0), (0,0), (0,0),
                (0,0), (0,0), (0,0), (0,0)],
        num_bins = N_bins,
        f = (P_px_px, P_nx_nx, P_px_nx, P_nx_px,
             P_py_py, P_ny_ny, P_py_ny, P_ny_py,
             P_px_py, P_nx_ny, P_px_ny, P_nx_py,
             P_py_px, P_ny_nx, P_py_nx, P_ny_px) -&gt; (P_px_px + P_nx_nx + P_px_nx + P_nx_px +
                                                     P_py_py + P_ny_ny + P_py_ny + P_ny_py -
                                                     P_px_py - P_nx_ny - P_px_ny - P_nx_py -
                                                     P_py_px - P_ny_nx - P_py_nx - P_ny_px)/4
    )
    additional_info[&quot;P_d_avg&quot;] = Pd
    additional_info[&quot;P_d_err&quot;] = ΔPd

    # Calculate the charge susceptibility for zero momentum transfer (q=0)
    # with the net charge background signal subtracted off.
    C0, ΔC0 = composite_correlation_stat(
        folder = simulation_info.datafolder,
        correlations = [&quot;density&quot;, &quot;greens&quot;],
        spaces = [&quot;momentum&quot;, &quot;position&quot;],
        types = [&quot;integrated&quot;, &quot;time-displaced&quot;],
        ids = [(1,1), (1,1)],
        locs = [(0,0), (0,0)],
        Δls = [0, 0],
        num_bins = N_bins,
        f = (x, y) -&gt; (x - β*N*(2*(1-y))^2)
    )
    additional_info[&quot;Chi_C_q0_avg&quot;] = C0
    additional_info[&quot;Chi_C_q0_err&quot;] = ΔC0

    # Write simulation summary TOML file.
    save_simulation_info(simulation_info, additional_info)

    return nothing
end


# Only excute if the script is run directly from the command line.
if abspath(PROGRAM_FILE) == @__FILE__

    # Read in the command line arguments.
    sID = parse(Int, ARGS[1]) # simulation ID
    U = parse(Float64, ARGS[2])
    Ω = parse(Float64, ARGS[3])
    α = parse(Float64, ARGS[4])
    μ = parse(Float64, ARGS[5])
    β = parse(Float64, ARGS[6])
    L = parse(Int, ARGS[7])
    N_burnin = parse(Int, ARGS[8])
    N_updates = parse(Int, ARGS[9])
    N_bins = parse(Int, ARGS[10])

    # Run the simulation.
    run_hubbard_holstein_square_simulation(sID, U, Ω, α, μ, β, L, N_burnin, N_updates, N_bins)
end</code></pre><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../bssh_chain/">« Bond Su-Schrieffer-Heeger Chain</a><a class="docs-footer-nextpage" href="../hubbard_threeband/">Three-Band Hubbard Model »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.25 on <span class="colophon-date" title="Tuesday 31 October 2023 13:00">Tuesday 31 October 2023</span>. Using Julia version 1.9.3.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
